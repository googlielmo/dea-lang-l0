/*
 * SPDX-License-Identifier: MIT OR Apache-2.0
 * Copyright (c) 2026 gwz
 */

module util.path;

import std.string;
import std.text;

func up_is_sep(c: byte) -> bool {
    return c == '/' || c == '\\';
}

func up_is_absolute(path: string) -> bool {
    let n = len_s(path);
    if (n == 0) {
        return false;
    }
    if (up_is_sep(char_at_s(path, 0))) {
        return true;
    }
    if (n >= 3 && char_at_s(path, 1) == ':' && up_is_sep(char_at_s(path, 2))) {
        return true;
    }
    return false;
}

func up_find_last_sep(path: string) -> int {
    let n = len_s(path);
    let last = -1;
    for (let i = 0; i < n; i = i + 1) {
        if (up_is_sep(char_at_s(path, i))) {
            last = i;
        }
    }
    return last;
}

func up_has_parent(path: string) -> bool {
    return up_find_last_sep(path) >= 0;
}

func up_basename(path: string) -> string {
    let n = len_s(path);
    if (n == 0) {
        return path;
    }

    let end = n;
    while (end > 1 && up_is_sep(char_at_s(path, end - 1))) {
        end = end - 1;
    }

    let last = -1;
    for (let i = 0; i < end; i = i + 1) {
        if (up_is_sep(char_at_s(path, i))) {
            last = i;
        }
    }

    return slice_s(path, last + 1, end);
}

func up_parent(path: string) -> string {
    let n = len_s(path);
    if (n == 0) {
        return ".";
    }

    let end = n;
    while (end > 1 && up_is_sep(char_at_s(path, end - 1))) {
        end = end - 1;
    }

    let last = -1;
    for (let i = 0; i < end; i = i + 1) {
        if (up_is_sep(char_at_s(path, i))) {
            last = i;
        }
    }

    if (last < 0) {
        return ".";
    }
    if (last == 0) {
        return slice_s(path, 0, 1);
    }

    if (len_s(path) >= 3 && char_at_s(path, 1) == ':' && last == 2) {
        return slice_s(path, 0, 3);
    }

    return slice_s(path, 0, last);
}

func up_is_l0_file(path: string) -> bool {
    return ends_with_s(path, ".l0");
}

func up_stem(path: string) -> string {
    let base = up_basename(path);
    if (up_is_l0_file(base)) {
        return slice_s(base, 0, len_s(base) - 3);
    }
    return base;
}

func up_join(root: string, rel: string) -> string {
    if (len_s(root) == 0) {
        return rel;
    }

    let tail = char_at_s(root, len_s(root) - 1);
    if (up_is_sep(tail)) {
        return concat_s(root, rel);
    }
    return concat3_s(root, "/", rel);
}
